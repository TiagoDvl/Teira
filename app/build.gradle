plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 33
    namespace 'br.com.tick.teira'

    defaultConfig {
        applicationId "br.com.tick.teira"
        minSdk 29
        targetSdk 33
        versionCode 2
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        qa {

        }
    }

    signingConfigs {
        release {
            def tmpFilePath = System.getProperty("user.home") + "/work/_temp/keystore/"
            def allFilesFromDir = new File(tmpFilePath).listFiles()
            if (allFilesFromDir != null) {
                def keystoreFile = allFilesFromDir.first()
                keystoreFile.renameTo("keystore/your_keystore.jks")
            }

            storeFile = file("keystore/your_keystore.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion composeVersion
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    // This allows us to use experimental coroutine components without warnings
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
        kotlinOptions.freeCompilerArgs += [
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
        ]
    }
    hilt {
        enableAggregatingTask = true
    }
}

dependencies {

    implementation project(path: ':sdk')
    implementation project(path: ':ui')

    implementation "androidx.core:core-ktx:$coreKtxVersion"
    implementation "com.google.firebase:firebase-crashlytics:$firebaseCrashlytics"
    implementation "com.google.firebase:firebase-analytics-ktx:$firebaseAnalytics"
    debugImplementation "androidx.compose.ui:ui-tooling:$composeVersion"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$composeVersion"


    implementation "com.google.dagger:hilt-android:$hiltVersion"
    implementation "androidx.hilt:hilt-navigation-compose:$hiltNavigationComposeVersion"
    implementation "androidx.hilt:hilt-work:$hiltWorkVersion"
    implementation "androidx.work:work-runtime-ktx:$workManagerVersion"

    kapt "androidx.hilt:hilt-compiler:$hiltCompilerVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    // Unit Tests dependencies
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$ktxCoroutinesTestVersion"
    testImplementation "app.cash.turbine:turbine:$turbineVersion"
    testImplementation "com.google.truth:truth:$truthVersion"

    // Android tests
    androidTestImplementation "androidx.test.ext:junit:$androidXJUnitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$composeVersion"
}
